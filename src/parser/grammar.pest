// pointer FIXME

program = _{ SOI ~ stmt* ~ EOI }
COMMENT = _{ "\\\\" ~ (!NEWLINE ~ ASCII)* ~ NEWLINE | "/*" ~ (!"*/" ~ ASCII)* ~ "*/" }
WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
ASCII = _{ '\u{00}'..'\u{7F}' }
n_ascii = _{ !ASCII_ALPHANUMERIC }
scope = { "{" ~ stmt* ~ "}" }
return_ = @{ "return" ~ n_ascii }
ez = @{ "ez" ~ n_ascii }
for_ = @{ "for" ~ n_ascii }
while_ = @{ "while" ~ n_ascii }
otherwise = @{ "otherwise" ~ n_ascii }
if_ = @{ "if" ~ n_ascii }
out_ = @{ "out" ~ n_ascii }
let_ = @{ "let" ~ n_ascii }

stmt = {
    assign
    | reassign
    | out
    | if_stmt
    | while_stmt
    | for_stmt
    | function
    | databox
    | return_stmt
    | expr
    | scope
}
keyword = {
    ("byte"
    | "bool"
    | "let"
    | "static"
    | "return"
    | "true"
    | "false"
    | "out"
    | "while"
    | "for"
    | "if"
    | "otherwise"
    | "ez"
    | "ezdata"
    | "input") ~ n_ascii
}
kind = {
    "bool" ~ n_ascii
    | "byte" ~ n_ascii
    | ";"
    | "ez" ~ "(" ~ (kind ~ ("," ~ kind)*)? ~ ")" ~ ("->" ~ kind)?
    | databox_expr
    | "&" ~ kind
    | ident
}

ident = @{
    !keyword
    ~ (ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*
    | "_" ~ (ASCII_ALPHANUMERIC | "_")+)
    | placeholder
}
placeholder = { "_" }
boolean = { ("true" | "false") ~ n_ascii }
none = { ";" }
input = { "in" ~ n_ascii }
string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ string_letter* }
string_letter = {
    !("\"" | "\\") ~ ASCII
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | "'" )
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
char = ${ "'" ~ char_letter ~ "'" }
char_letter = {
    !("'" | "\\") ~ ASCII
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | "'" )
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
number = @{"0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
array = { "[" ~ (expr ~ ("," ~ expr)*)? ~ "]" }

value = _{
    number
    | boolean
    | none
    | string
    | char
    | array
    | input
    | ident
    | "(" ~ expr ~ ")"
}
expr = {
    type_cast
    | unop_expr
    | ternary
    | databox_const
    | binop_expr
    | increment
    | decrement
    | function_expr
    | databox_expr
    | attr_access
    | index
    | call
    | value
}

unop_expr = { unop ~ expr }
ternary = { value ~ "?" ~ expr ~ ":" ~ expr }  //! FIXME: change value to expr
type_cast = { "<" ~ kind ~ ">" ~ expr }
databox_const = {  //! FIXME: change ident to expr
    (ident | databox_expr) ~ "{"
        ~ (ident ~ ":" ~ expr
        ~ ("," ~ ident ~ ":" ~ expr)*)?
    ~ "}"
}
binop_expr = { value ~ binop ~ expr }  //! FIXME: change value to expr
call = { value ~ "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }  //! FIXME: change value to expr
attr_access = { value ~ "." ~ ident }  //! FIXME: change 1st value to expr
index = { value ~ "[" ~ expr ~ "]" }  //! FIXME: change value to expr
increment = { value ~ "++" }  //! FIXME: change value to expr
decrement = { value ~ "--" }  //! FIXME: change value to expr
function_expr = {
    "ez" ~ "(" 
    ~ (ident ~ ":" ~ kind
    ~ ("," ~ ident ~ ":" ~ kind)*)? 
    ~ ")"
    ~ ("->" ~ kind)?
    ~ stmt
}
databox_expr = {
    "ez" ~ "{"
        ~ (ident ~ ":" ~ kind
        ~ ("," ~ ident ~ ":" ~ kind)*)?
    ~ "}"
}

binop = {(
    "+"
    | "-"
    | "*"
    | "/"
    | "%"
    | "|"
    | "^"
    | "&"
    | "**"
    | ">>"
    | "<<"
    | "=="
    | "!="
    | ">="
    | "<="
    | ">"
    | "<"
)}
unop = {(
    "&"
    | "-"
    | "!"
    | "*"
)}

assign = { let_ ~ ident ~ (":" ~ kind)? ~ "=" ~ expr }
reassign = {
    ident
    ~ ("="
        | "+="
        | "-="
        | "/="
        | "%="
        | "**="
        | ">>="
        | "<<="
        | "&="
        | "|="
        | "^="
    )
    ~ expr
}
out = { out_ ~ expr }
return_stmt = { return_ ~ expr }

if_stmt = {
    if_ ~ expr ~ stmt
    ~ (otherwise
    ~   stmt
    )?
}
while_stmt = { while_ ~ expr ~ stmt }
for_stmt = {
    for_ ~ ":"
    ~    stmt ~ ":" ~ expr ~ ":" ~ stmt 
    ~ ":" ~ stmt
}
function = {
    ez ~ ident ~ "(" 
    ~ (ident ~ ":" ~ kind
    ~ ("," ~ ident ~ ":" ~ kind)*)? 
    ~ ")"
    ~ ("->" ~ kind)?
    ~ stmt
}
databox = {
    ez ~ ident ~ "{"
        ~ (ident ~ ":" ~ kind
        ~ ("," ~ ident ~ ":" ~ kind)*)?
    ~ "}"
}
